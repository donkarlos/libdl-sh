dnl								-*-Autotest-*-
dnl libdl-sh.at			- libdl-sh(1) tests
dnl
dnl Copyright © 2021 Das Computerlabor (DCl-M)
dnl
dnl This library is free software; you can redistribute it and/or
dnl modify it under the terms of the GNU Lesser General Public License
dnl as published by the Free Software Foundation; either
dnl version 2.1 of the License, or (at your option) any later version.
dnl
dnl This library is distributed in the hope that it will be useful,
dnl but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
dnl Lesser General Public License for more details.
dnl
dnl You should have received a copy of the GNU Lesser General Public License
dnl along with this library; if not, write to the Free Software Founda-
dnl tion, Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA
dnl
dnl AUTHOR(S):	ks	Karl Schmitz <ks@computerlabor.org>
dnl
dnl WRITTEN BY:	ks	2021-05-03
dnl CHANGED BY:	ks	2021-05-04	Execute libdl-sh(1) tests under
dnl					strict (i.e., »set -eu«) conditions.
dnl					Don't augment LSP during install
dnl					checks.
dnl		ks	2021-05-07	Always expect no error output.
dnl
dnl Test group »Demand-loading helper libdl-sh(1)«
dnl
AT_TESTED([libdl-sh])
AT_BANNER([Demand-loading helper libdl-sh(1)])

AT_SETUP([»libdl-sh exec« prepends to $PATH at all?])
AT_KEYWORDS([libdl-sh])
AT_CHECK([dnl
set -eu
case `libdl-sh exec -- /bin/sh -c 'echo "$PATH"'` in
    ?*:$PATH)
	;;
    *)	exit 1
	;;
esac
], 0, [ignore], [dnl
])
AT_CLEANUP

AT_SETUP([»libdl-sh exec« prepends ${builddir}/bin to $PATH?])
AT_KEYWORDS([libdl-sh])
AT_CHECK([dnl
set -eu
case :$PATH: in				## Triggered by »check-local:«?
    *:$abs_top_builddir/bin:*)		## Yes…
	case `libdl-sh exec -- /bin/sh -c 'echo "$PATH"'` in
	    $abs_top_builddir/bin:$PATH)
		;;
	    *)	exit 1
		;;
	esac
	;;
esac
], 0, [ignore], [dnl
])
AT_CLEANUP

AT_SETUP([»libdl-sh load« honours $LIBDL_SH_LIBRARY_PATH?])
AT_KEYWORDS([libdl-sh])
AT_CHECK([dnl
set -eu
SQ=\'
case :$PATH: in				## Triggered by »check-local:«?
    *:$abs_top_builddir/bin:*)		## Yes…
	LIBDL_SH_LIBRARY_PATH=$abs_top_builddir/lib
	LIBDL_SH_LIBEXECDIR=$abs_top_builddir/libexec
	export LIBDL_SH_LIBRARY_PATH LIBDL_SH_LIBEXECDIR
	case `libdl-sh load` in
	    '. '"$SQ$abs_top_builddir"'/lib/libdl.sh'"$SQ"' || exit 127')
		;;
	    *)	exit 1
		;;
	esac
	;;
esac
], 0, [ignore], [dnl
])
AT_CLEANUP

AT_SETUP([»eval "`libdl-sh load`"« loads libdl.sh?])
AT_KEYWORDS([libdl-sh])
AT_CHECK([dnl
set -eu
case :$PATH: in				## Triggered by »check-local:«?
    *:$abs_top_builddir/bin:*)		## Yes…
	LIBDL_SH_LIBRARY_PATH=$abs_top_builddir/lib
	LIBDL_SH_LIBEXECDIR=$abs_top_builddir/libexec
	export LIBDL_SH_LIBRARY_PATH LIBDL_SH_LIBEXECDIR
	;;
esac
eval "`libdl-sh load`"
], 0, [ignore], [dnl
])
AT_CLEANUP
