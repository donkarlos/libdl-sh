dnl								-*-Autotest-*-
dnl libsafe.sh.at		- libsafe.sh tests
dnl
dnl Copyright © 2021-2022 Das Computerlabor (DCl-M)
dnl
dnl This library is free software; you can redistribute it and/or
dnl modify it under the terms of the GNU Lesser General Public License
dnl as published by the Free Software Foundation; either
dnl version 2.1 of the License, or (at your option) any later version.
dnl
dnl This library is distributed in the hope that it will be useful,
dnl but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
dnl Lesser General Public License for more details.
dnl
dnl You should have received a copy of the GNU Lesser General Public License
dnl along with this library; if not, write to the Free Software Founda-
dnl tion, Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA
dnl
dnl AUTHOR(S):	ks	Karl Schmitz <ks@computerlabor.org>
dnl
dnl WRITTEN BY:	ks	2021-05-13
dnl CHANGED BY:	ks	2021-05-23	Simplify atlocal_function_deprecated()
dnl					and atlocal_function_withdrawn().
dnl		ks	2021-05-24	Use atlocal_check_meta_inquiries(),
dnl					atlocal_trace_invocation_set_output(),
dnl					atlocal_trace_invocation_raw_output(),
dnl					atlocal_check_constants(), and
dnl					atlocal_check_function_usage_errors().
dnl		ks	2021-05-25	Make constant's API table entries API
dnl					version-independent.
dnl					Use atlocal_check_meta_get_apis_hon-
dnl					ours_4th().
dnl		ks	2021-05-26	Add test groups [»safe_meta set 0tag«
dnl					works as expected?] and [safe_init()
dnl					works as expected?].
dnl		ks	2021-05-30	Add test group ['safe_meta get apis
dnl					all' declarations sane?].
dnl					Use atlocal_check_api_consistency().
dnl		ks	2022-05-08	Make LIB_meta() tests AC_INIT()-inde-
dnl					pendent.
dnl
dnl Test group »Safe directory/file operations library libsafe.sh«
dnl
AT_BANNER([Safe directory/file operations library libsafe.sh])

dnl
dnl Check if »dl_load safe« loads libsafe.sh
dnl
AT_SETUP([»dl_load safe« loads libsafe.sh?])
AT_KEYWORDS([libsafe.sh libsafe dl_load])
AT_CHECK([dnl
set -eu					## Employ strict execution environment
					## to ensure »set -u« resilience.
test_atload_trace=:			## Initialize load-time hook's trace.
test_atload() {				## Declare load-time hook.
    test_atload_trace=:$1$test_atload_trace
}

test_atexit() {				## Declare exit-time hook.
    echo "test_atexit($status)"
    echo "$test_atload_trace"
}

case :$PATH: in				## Triggered by »check-local:«?
    *:$abs_top_builddir/bin:*)		## Yes, export suitable LSP.
	LIBDL_SH_LIBRARY_PATH=$abs_top_builddir/lib
	LIBDL_SH_LIBEXECDIR=$abs_top_builddir/libexec
	export LIBDL_SH_LIBRARY_PATH LIBDL_SH_LIBEXECDIR
	;;
esac
eval "`libdl-sh load`"			## Load libdl.sh or die.
dl_atload test_atload && dl_atexit test_atexit
dl_load safe				## Register hooks and load
					## libsafe.sh.
], 0, [dnl				## And the winner is…
test_atexit(0)
:safe:undo:quote:dl:
], [dnl
])
AT_CLEANUP

dnl
dnl Check if safe_meta() inquiries
dnl   -	get package
dnl   -	get version
dnl   -	get api
dnl   -	get apis
dnl   -	get apis api
dnl   -	get apis full
dnl   -	get apis list
dnl   -	get apis r/o
dnl produce expected results.
dnl
dnl NOTE:   (1)	This test wraps around atlocal_check_meta_inquiries()!
dnl	    (2)	The double brackets within the expected output provide "just
dnl		the right amount" of m4(1) quoting.
dnl
AT_SETUP([»safe_meta get« inquiries give expected results?])
AT_KEYWORDS([libsafe.sh libsafe safe_meta])
AT_CHECK([dnl
set -eu					## Employ strict execution environment
					## to ensure »set -u« resilience.
case :$PATH: in				## Triggered by »check-local:«?
    *:$abs_top_builddir/bin:*)		## Yes, export suitable LSP.
	LIBDL_SH_LIBRARY_PATH=$abs_top_builddir/lib
	LIBDL_SH_LIBEXECDIR=$abs_top_builddir/libexec
	export LIBDL_SH_LIBRARY_PATH LIBDL_SH_LIBEXECDIR
	;;
esac
eval "`libdl-sh load`"; dl_load safe	## Load libdl.sh and libsafe.sh or die.
atlocal_check_meta_inquiries safe	## Perform check for libsafe.sh.
], 0, [dnl				## And the winner is…
safe_meta([[2]]get,package)=0
@PACKAGE_TARNAME@
safe_meta([[2]]get,version)=0
@PACKAGE_VERSION@
safe_meta([[2]]get,api)=0
0
safe_meta([[2]]get,apis)=0
0
1
safe_meta([[3]]get,apis,api)=0
0
1
safe_meta([[3]]get,apis,full)=0
0;safe_meta(META [[ARG …]]);2-4
0;safe_init(TAG);1
0;safe_mktmpdir(&DPN [[DRN]]);1-2
0;safe_mkdir(DPN …);1-
0;safe_chdir(DPN);1
0;safe_touch(FPN …);1-
0;safe_save(FPN);1
0;safe_rename(OLDPN NEWPN);2
0;safe_remove(PN …);1-
0;safe_umask(UMASK COMMAND [[ARGUMENT …]]);2-
0;safe_unlock(PN);1
0;safe_lock(PN);1
0;safe_wait(PN [[SECONDS=30]]);1-2
1;safe_meta(META [[ARG …]]);2-4
1;safe_mktmpdir(&DPN [[DRN]]);1-2
1;safe_mkdir(DPN …);1-
1;safe_chdir(DPN);1
1;safe_touch(FPN …);1-
1;safe_save(FPN);1
1;safe_rename(OLDPN NEWPN);2
1;safe_remove(PN …);1-
1;safe_umask(UMASK COMMAND [[ARGUMENT …]]);2-
1;safe_unlock(PN);1
1;safe_lock(PN);1
1;safe_wait(PN [[SECONDS=30]]);1-2
safe_meta([[3]]get,apis,list)=0
0;safe_meta;2-4
0;safe_init;1
0;safe_mktmpdir;1-2
0;safe_mkdir;1-
0;safe_chdir;1
0;safe_touch;1-
0;safe_save;1
0;safe_rename;2
0;safe_remove;1-
0;safe_umask;2-
0;safe_unlock;1
0;safe_lock;1
0;safe_wait;1-2
1;safe_meta;2-4
1;safe_mktmpdir;1-2
1;safe_mkdir;1-
1;safe_chdir;1
1;safe_touch;1-
1;safe_save;1
1;safe_rename;2
1;safe_remove;1-
1;safe_umask;2-
1;safe_unlock;1
1;safe_lock;1
1;safe_wait;1-2
safe_meta([[3]]get,apis,r/o)=0
], [dnl
])
AT_CLEANUP

dnl
dnl Check if the API version and function declarations reported by 'safe_meta
dnl get apis all' are consistent and that the reported functions are properly
dnl deprecated/withdrawn.
dnl
dnl NOTE:   (1)	The double brackets within the expected output provide "just
dnl		the right amount" of m4(1) quoting.
dnl
AT_SETUP([»safe_meta get apis all« declarations sane?])
AT_KEYWORDS([libsafe.sh libsafe safe_meta consistent deprecated withdrawn])
AT_CHECK([dnl
set -eu					## Employ strict execution environment
					## to ensure »set -u« resilience.
test_hook() {				## (TESTER LIB API TESTEE)
    case ${5+5}${4+=$4} in		## Usage OK?/Function?
	=safe_init)			## Y/safe_init():
	    "$@" :TAG			## Test it with reasonable arguments.
	    ;;
	=${2-}|=${2-}_*)		## Y/Any recently deprecated/withdrawn:
	    echo "$4() deprecated or withdrawn!" >&2
	    ;;				## Emit unexpected error message!
	*)  return 2			## N/?: Indicate abuse!
	    ;;
    esac
}

case :$PATH: in				## Triggered by »check-local:«?
    *:$abs_top_builddir/bin:*)		## Yes, export suitable LSP.
	LIBDL_SH_LIBRARY_PATH=$abs_top_builddir/lib
	LIBDL_SH_LIBEXECDIR=$abs_top_builddir/libexec
	export LIBDL_SH_LIBRARY_PATH LIBDL_SH_LIBEXECDIR
	;;
esac
eval "`libdl-sh load`"; dl_load safe	## Load libdl.sh and libsafe.sh or die.
atlocal_check_api_consistency safe test_hook
					## Perform check for libsafe.sh.
], 0, [dnl				## And the winner is…
Declared API versions: [[0;1]]
safe_meta([[3]]set,api,0)=0
safe_init([[1]]:TAG)=126
safe_meta([[3]]set,api,1)=0
safe_init([[1]]:TAG)=127
], [dnl
])
AT_CLEANUP

dnl
dnl Check if safe_meta() inquiries
dnl   -	get apis full
dnl   -	get apis list
dnl   -	get apis r/o
dnl hounour (i.e., apply) the extra sed(1) expression passed as 4th argument.
dnl
dnl NOTE:   (1)	This test wraps around atlocal_check_meta_get_apis_honours_-
dnl		4th()!
dnl	    (2)	The double brackets within the expected output provide "just
dnl		the right amount" of m4(1) quoting.
dnl	    (3)	The expected output contains one quadrigraph to account for
dnl		the unmatched »(« in »full« format's sed(1) expression!
dnl
AT_SETUP([»safe_meta get apis« honours 4th argument?])
AT_KEYWORDS([libsafe.sh libsafe safe_meta])
AT_CHECK([dnl
set -eu					## Employ strict execution environment
					## to ensure »set -u« resilience.
case :$PATH: in				## Triggered by »check-local:«?
    *:$abs_top_builddir/bin:*)		## Yes, export suitable LSP.
	LIBDL_SH_LIBRARY_PATH=$abs_top_builddir/lib
	LIBDL_SH_LIBEXECDIR=$abs_top_builddir/libexec
	export LIBDL_SH_LIBRARY_PATH LIBDL_SH_LIBEXECDIR
	;;
esac
eval "`libdl-sh load`"; dl_load safe	## Load libdl.sh and libsafe.sh or die.
atlocal_check_meta_get_apis_honours_4th safe safe_mkdir
					## Perform check for libsafe.sh.
], 0, [dnl				## And the winner is…
safe_meta([[4]]get,apis,full,/;safe_mkdir@{:@/!d;s/;.*$//)=0
0
1
safe_meta([[4]]get,apis,list,/;safe_mkdir;/!d;s/;.*$//)=0
0
1
safe_meta([[4]]get,apis,r/o,/^safe_/!d;s///)=0
], [dnl
])
AT_CLEANUP

dnl
dnl Check if the constants reported by 'safe_meta get apis r/o (for the
dnl default API version) are defined and readonly after loading libsafe.sh.
dnl
dnl NOTE:   (1)	This test wraps around atlocal_check_constants()!
dnl	    (2)	The double brackets within the expected output provide "just
dnl		the right amount" of m4(1) quoting.
dnl
AT_SETUP([»safe_meta get apis r/o« var.s set and readonly?])
AT_KEYWORDS([libsafe.sh libsafe safe_meta constants readonly])
AT_CHECK([dnl
set -eu					## Employ strict execution environment
					## to ensure »set -u« resilience.
case :$PATH: in				## Triggered by »check-local:«?
    *:$abs_top_builddir/bin:*)		## Yes, export suitable LSP.
	LIBDL_SH_LIBRARY_PATH=$abs_top_builddir/lib
	LIBDL_SH_LIBEXECDIR=$abs_top_builddir/libexec
	export LIBDL_SH_LIBRARY_PATH LIBDL_SH_LIBEXECDIR
	;;
esac
eval "`libdl-sh load`"; dl_load safe	## Load libdl.sh and libsafe.sh or die.
atlocal_check_constants safe		## Perform check for libsafe.sh.
], 0, [dnl				## And the winner is…
], [dnl
])
AT_CLEANUP

dnl
dnl Check if the functions reported by »safe_meta get apis list« (for all API
dnl versions) indicate usage errors (i.e., return 2) when invoked with
dnl insufficient arguments.
dnl
dnl NOTE:   (1)	This test wraps around atlocal_check_function_usage_errors()!
dnl	    (2)	The double brackets within the expected output provide "just
dnl		the right amount" of m4(1) quoting.
dnl
AT_SETUP([libsafe.sh functions indicate usage errors?])
AT_KEYWORDS([libsafe.sh libsafe usage])
AT_CHECK([dnl
set -eu					## Employ strict execution environment
					## to ensure »set -u« resilience.
case :$PATH: in				## Triggered by »check-local:«?
    *:$abs_top_builddir/bin:*)		## Yes, export suitable LSP.
	LIBDL_SH_LIBRARY_PATH=$abs_top_builddir/lib
	LIBDL_SH_LIBEXECDIR=$abs_top_builddir/libexec
	export LIBDL_SH_LIBRARY_PATH LIBDL_SH_LIBEXECDIR
	;;
esac
eval "`libdl-sh load`"; dl_load safe	## Load libdl.sh and libsafe.sh or die.
atlocal_check_function_usage_errors safe
					## Perform check for libsafe.sh.
], 0, [dnl				## And the winner is…
safe_meta([[3]]set,api,0)=0
safe_meta([[0]])=2
safe_meta([[1]]1)=2
safe_init([[0]])=2
safe_mktmpdir([[0]])=2
safe_mkdir([[0]])=2
safe_chdir([[0]])=2
safe_touch([[0]])=2
safe_save([[0]])=2
safe_rename([[0]])=2
safe_rename([[1]]1)=2
safe_remove([[0]])=2
safe_umask([[0]])=2
safe_umask([[1]]1)=2
safe_unlock([[0]])=2
safe_lock([[0]])=2
safe_wait([[0]])=2
safe_meta([[3]]set,api,1)=0
safe_meta([[0]])=2
safe_meta([[1]]1)=2
safe_mktmpdir([[0]])=2
safe_mkdir([[0]])=2
safe_chdir([[0]])=2
safe_touch([[0]])=2
safe_save([[0]])=2
safe_rename([[0]])=2
safe_rename([[1]]1)=2
safe_remove([[0]])=2
safe_umask([[0]])=2
safe_umask([[1]]1)=2
safe_unlock([[0]])=2
safe_lock([[0]])=2
safe_wait([[0]])=2
], [dnl
])
AT_CLEANUP

dnl
dnl Check if »safe_meta set 0tag« works as expected.
dnl
dnl NOTE:   (1)	The double brackets within the expected output provide "just
dnl		the right amount" of m4(1) quoting.
dnl
AT_SETUP([»safe_meta set 0tag« works as expected?])
AT_KEYWORDS([libsafe.sh libsafe safe_meta cleanups])
AT_CHECK([dnl
set -eu					## Employ strict execution environment
					## to ensure »set -u« resilience.
case :$PATH: in				## Triggered by »check-local:«?
    *:$abs_top_builddir/bin:*)		## Yes, export suitable LSP.
	LIBDL_SH_LIBRARY_PATH=$abs_top_builddir/lib
	LIBDL_SH_LIBEXECDIR=$abs_top_builddir/libexec
	export LIBDL_SH_LIBRARY_PATH LIBDL_SH_LIBEXECDIR
	;;
esac
eval "`libdl-sh load`"; dl_load safe	## Load libdl.sh and libsafe.sh or die.
status=0				## Assume success.
atlocal_trace_variable safe_0tag
atlocal_trace_invocation_returns 0 safe_meta set 0tag :0	|| status=1
atlocal_trace_variable safe_0tag
atlocal_trace_invocation_returns 0 safe_meta set 0tag :0	|| status=1
atlocal_trace_variable safe_0tag
atlocal_trace_invocation_returns 1 safe_meta set 0tag :1	|| status=1
atlocal_trace_variable safe_0tag
exit $status				## Indicate success or failure.
], 0, [dnl				## And the winner is…
unset safe_0tag
safe_meta([[3]]set,0tag,:0)=0
safe_0tag=:0
safe_meta([[3]]set,0tag,:0)=0
safe_0tag=:0
safe_meta([[3]]set,0tag,:1)=1
safe_0tag=:0
], [dnl
])
AT_CLEANUP

dnl
dnl Check if safe_init() works as expected.
dnl
dnl NOTE:   (1)	The double brackets within the expected output provide "just
dnl		the right amount" of m4(1) quoting.
dnl
AT_SETUP([safe_init() works as expected?])
AT_KEYWORDS([libsafe.sh libsafe safe_init cleanups])
AT_CHECK([dnl
set -eu					## Employ strict execution environment
					## to ensure »set -u« resilience.
case :$PATH: in				## Triggered by »check-local:«?
    *:$abs_top_builddir/bin:*)		## Yes, export suitable LSP.
	LIBDL_SH_LIBRARY_PATH=$abs_top_builddir/lib
	LIBDL_SH_LIBEXECDIR=$abs_top_builddir/libexec
	export LIBDL_SH_LIBRARY_PATH LIBDL_SH_LIBEXECDIR
	;;
esac
eval "`libdl-sh load`"; dl_load safe	## Load libdl.sh and libsafe.sh or die.
status=0				## Assume success.
atlocal_trace_invocation_returns 0 safe_meta set api 0	|| status=1
atlocal_trace_variable safe_0tag
atlocal_trace_invocation_returns 0 safe_init :0		|| status=1
atlocal_trace_variable safe_0tag
atlocal_trace_invocation_returns 0 safe_init :0		|| status=1
atlocal_trace_variable safe_0tag
atlocal_trace_invocation_returns 1 safe_init :1		|| status=1
atlocal_trace_variable safe_0tag
exit $status				## Indicate success or failure.
], 0, [dnl				## And the winner is…
safe_meta([[3]]set,api,0)=0
unset safe_0tag
safe_init([[1]]:0)=0
safe_0tag=:0
safe_init([[1]]:0)=0
safe_0tag=:0
safe_init([[1]]:1)=1
safe_0tag=:0
], [dnl
])
AT_CLEANUP
