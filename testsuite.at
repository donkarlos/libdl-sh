dnl								-*-Autotest-*-
dnl testsuite.at		- libdl-sh test suite
dnl
dnl Copyright Â© 2021 Das Computerlabor (DCl-M)
dnl
dnl This library is free software; you can redistribute it and/or
dnl modify it under the terms of the GNU Lesser General Public License
dnl as published by the Free Software Foundation; either
dnl version 2.1 of the License, or (at your option) any later version.
dnl
dnl This library is distributed in the hope that it will be useful,
dnl but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
dnl Lesser General Public License for more details.
dnl
dnl You should have received a copy of the GNU Lesser General Public License
dnl along with this library; if not, write to the Free Software Founda-
dnl tion, Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA
dnl
dnl AUTHOR(S):	ks	Karl Schmitz <ks@computerlabor.org>
dnl
dnl WRITTEN BY:	ks	2021-04-28
dnl CHANGED BY:
dnl
dnl Initialization:
dnl
AT_INIT()
AT_COLOR_TESTS

dnl
dnl Test group 'Demand-loading helper libdl-sh(1)'
dnl
AT_BANNER([Demand-loading helper libdl-sh(1)])

AT_SETUP([libdl-sh exec prepends to PATH?])
AT_CHECK([dnl
case `libdl-sh exec /bin/sh -c 'echo "$PATH"'` in
    ?*:$PATH)
	;;
    *)	exit 1
	;;
esac
], 0, [ignore], [ignore])
AT_CLEANUP

AT_SETUP([libdl-sh exec prepends build bin/ to PATH?])
AT_CHECK([dnl
case :$PATH: in
    *:$abs_top_builddir/bin:*)
	case `libdl-sh exec /bin/sh -c 'echo "$PATH"'` in
	    $abs_top_builddir/bin:$PATH)
		;;
	    *)	exit 1
		;;
	esac
	;;
esac
], 0, [ignore], [ignore])
AT_CLEANUP

AT_SETUP([libdl-sh load respects LIBDL_SH_LIBRARY_PATH?])
AT_CHECK([dnl
SQ=\'
case `LIBDL_SH_LIBRARY_PATH=$abs_top_builddir/lib libdl-sh load` in
    '. '"$SQ$abs_top_builddir"'/lib/libdl.sh'"$SQ"' || exit 127')
	;;
    *)	exit 1
	;;
esac
], 0, [ignore], [ignore])
AT_CLEANUP

AT_SETUP([eval "`libdl-sh load`" loads libdl.sh?])
AT_CHECK([dnl
LIBDL_SH_LIBRARY_PATH=$abs_top_builddir/lib; export LIBDL_SH_LIBRARY_PATH
eval "`libdl-sh load`"
], 0, [ignore], [ignore])
AT_CLEANUP

